import java.util.function.Consumer

plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.3.3'
}

group = 'net.forthecrown'
version = buildVersion()

compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenCentral()

    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://mvn.intellectualsites.com/content/repositories/releases/' }
    maven { url = 'https://libraries.minecraft.net' }
    maven { url = 'https://maven.playpro.com' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    //Paper and NMS dependency
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.18.1-R0.1-SNAPSHOT")

    //DO NO CHANGE VERSIONS, Paper has these exact versions shaded into it
    compileOnly 'io.netty:netty-all:4.1.68.Final'
    compileOnly 'io.papermc.paper:paper-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-mojangapi:1.18-R0.1-SNAPSHOT'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'

    compileOnly 'com.mojang:brigadier:1.0.18'
    compileOnly 'com.mojang:authlib:3.2.38'
    compileOnly 'com.mojang:datafixerupper:4.0.26'

    compileOnly 'org.mcteam.ancientgates:ancientgates:2.4'
    compileOnly 'net.coreprotect:coreprotect:20.4'

    //Annotations????
    compileOnly 'org.jetbrains:annotations:22.0.0'

    //LuckPerms and WorldGuard
    compileOnly 'com.github.lucko:LuckPerms:5.3'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5-SNAPSHOT'

    //Grenadier, Squire, FAWE
    libJar('Grenadier-dev')
    libJar('Squire')
    libJar('FAWE')
    libJar('DiscordSRV')
    libJar('Dynmap')
}

void libJar(String s) {
    dependencies.compileOnly(files('/libs/' + s + '.jar'))
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

processResources {
    File pluginYml = new File('build/resources/main/plugin.yml')
    if(pluginYml.exists()) pluginYml.delete()

    def props = [version: version, buildID : buildID() + "", buildDate : new Date().toString()]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


String buildVersion() {
    Calendar cal = Calendar.getInstance();
    int day = cal.get(Calendar.DAY_OF_MONTH);
    int year = cal.get(Calendar.YEAR);
    int month = cal.get(Calendar.MONTH) + 1;

    return year + "-" + month + "-" + day + "-" + buildID()
}

int buildID() {
    int result = 1

    consumeProperties(p -> {
        result = Integer.parseInt(p.getProperty("buildID"))
    })

    return result
}

void incrementBuildID() {
    consumeProperties(properties -> {
        int id = Integer.parseInt(properties.getProperty("buildID"))
        id++

        properties.setProperty("buildID", id + "")
    });
}

void consumeProperties(Consumer<Properties> propertiesConsumer) {
    File f = new File(project.projectDir, 'buildver.properties')
    Properties properties = new Properties()
    properties.load(new FileReader(f))

    propertiesConsumer.accept(properties)

    properties.store(new FileOutputStream(f), "Saves the last build ID, last build:")
}

assemble {
    archivesBaseName = 'ForTheCrown-Plugin'
    dependsOn(reobfJar)
}

build {
    incrementBuildID()
}